buildscript
{
    repositories
    {
        jcenter()
        maven
        {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven
        {
            name = "SpongePowered"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
    }
    dependencies
    {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

group = "me.gavin.notorious"
version = "0.3"
archivesBaseName = "Notorious"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

compileJava.options.encoding = "UTF-8"

minecraft
{
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    coreMod = "me.gavin.notorious.mixin.NotoriousMixinLoader"
}

repositories
{
    maven
    {
        name = "spongepowered-repo"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven
    {
        name = "swt-repo"
        url = "http://maven-eclipse.github.io/maven"
    }
    maven
    {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }

    mavenCentral()
    jcenter()
}

dependencies
{
    compile("org.spongepowered:mixin:0.7.4-SNAPSHOT")
    {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    compile 'club.minnced:java-discord-rpc:2.0.2'
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs)
    {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs)
    {
        exclude "mcmod.info"
    }
}

shadowJar
{
    dependencies
    {
        include(dependency("org.spongepowered:mixin"))
        include(dependency('club.minnced:java-discord-rpc'))
    }

    exclude "dummyThing"
    exclude "LICENSE.txt"
    classifier = "release"
}

mixin
{
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.notorious.refmap.json"
}

reobf
{
    shadowJar
    {
        mappingType = "SEARGE"
        classpath = sourceSets.main.compileClasspath
    }
}

jar
{
    manifest
    {
        attributes(
                "MixinConfigs": "mixins.notorious.json",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin": "me.gavin.notorious.mixin.NotoriousMixinLoader",
                "ForceLoadAsMod": "true",
        )
    }
}

build.dependsOn(shadowJar)
